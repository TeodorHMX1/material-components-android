apply plugin: 'com.android.library'

dependencies {
  api compatibility("annotation")
  api compatibility("appcompat")
  api compatibility("cardview")
  api compatibility("coordinatorlayout")
  api compatibility("core")
  api compatibility("experimental")
  api compatibility("fragment")
  api compatibility("lifecycleRuntime")
  api compatibility("recyclerview")
  api compatibility("transition")
  api compatibility("vectordrawable")
  api compatibility("viewpager2")
}

def srcDirs = [
  'com/zeoflow/material/animation',
  'com/zeoflow/material/appbar',
  'com/zeoflow/material/badge',
  'com/zeoflow/material/behavior',
  'com/zeoflow/material/bottomappbar',
  'com/zeoflow/material/bottomnavigation',
  'com/zeoflow/material/bottomsheet',
  'com/zeoflow/material/button',
  'com/zeoflow/material/canvas',
  'com/zeoflow/material/card',
  'com/zeoflow/material/checkbox',
  'com/zeoflow/material/chip',
  'com/zeoflow/material/circularreveal',
  'com/zeoflow/material/circularreveal/cardview',
  'com/zeoflow/material/circularreveal/coordinatorlayout',
  'com/zeoflow/material/color',
  'com/zeoflow/material/datepicker',
  'com/zeoflow/material/dialog',
  'com/zeoflow/material/drawable',
  'com/zeoflow/material/elevation',
  'com/zeoflow/material/expandable',
  'com/zeoflow/material/floatingactionbutton',
  'com/zeoflow/material/imageview',
  'com/zeoflow/material/internal',
  'com/zeoflow/material/math',
  'com/zeoflow/material/menu',
  'com/zeoflow/material/navigation',
  'com/zeoflow/material/radiobutton',
  'com/zeoflow/material/resources',
  'com/zeoflow/material/ripple',
  'com/zeoflow/material/shape',
  'com/zeoflow/material/shadow',
  'com/zeoflow/material/slider',
  'com/zeoflow/material/snackbar',
  'com/zeoflow/material/stateful',
  'com/zeoflow/material/switchmaterial',
  'com/zeoflow/material/tabs',
  'com/zeoflow/material/textfield',
  'com/zeoflow/material/textview',
  'com/zeoflow/material/theme',
  'com/zeoflow/material/theme/overlay',
  'com/zeoflow/material/tooltip',
  'com/zeoflow/material/transition',
  'com/zeoflow/material/transformation',
  'com/zeoflow/material/typography',
]

android {
  sourceSets {
    main.manifest.srcFile 'java/com/zeoflow/material/AndroidManifest.xml'
    main.java.srcDir 'java'
    main.java.includes = srcDirs.collect { it + '/**/*.java' }
    main.java.excludes = [
      '**/build/**',
    ]
    srcDirs.forEach {
      main.res.srcDirs += 'java/' + it + '/res'
      main.res.srcDirs += 'java/' + it + '/res-public'
    }
  }

  testOptions.unitTests.includeAndroidResources = true

  buildTypes.all {
    consumerProguardFiles 'proguard-behaviors.pro', 'proguard-inflater.pro'
  }

  compileSdkVersion rootProject.ext.compileSdkVersion
  defaultConfig {
    vectorDrawables.useSupportLibrary = true
    minSdkVersion rootProject.ext.minSdkVersion
    targetSdkVersion 28
  }

  testOptions {
    unitTests.returnDefaultValues = true
  }

  aaptOptions {
    additionalParameters "--no-version-vectors"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  dexOptions{
    preDexLibraries = rootProject.ext.preDexLibs
  }
}
