apply plugin: 'com.android.library'

dependencies {
  api compatibility("annotation")
  api compatibility("appcompat")
  api compatibility("cardview")
  api compatibility("coordinatorlayout")
  api compatibility("core")
  api compatibility("experimental")
  api compatibility("fragment")
  api compatibility("lifecycleRuntime")
  api compatibility("recyclerview")
  api compatibility("transition")
  api compatibility("vectordrawable")
  api compatibility("viewpager2")
}

def srcDirs = [
  'com/zeoflow/android/material/animation',
  'com/zeoflow/android/material/appbar',
  'com/zeoflow/android/material/badge',
  'com/zeoflow/android/material/behavior',
  'com/zeoflow/android/material/bottomappbar',
  'com/zeoflow/android/material/bottomnavigation',
  'com/zeoflow/android/material/bottomsheet',
  'com/zeoflow/android/material/button',
  'com/zeoflow/android/material/canvas',
  'com/zeoflow/android/material/card',
  'com/zeoflow/android/material/checkbox',
  'com/zeoflow/android/material/chip',
  'com/zeoflow/android/material/circularreveal',
  'com/zeoflow/android/material/circularreveal/cardview',
  'com/zeoflow/android/material/circularreveal/coordinatorlayout',
  'com/zeoflow/android/material/color',
  'com/zeoflow/android/material/datepicker',
  'com/zeoflow/android/material/dialog',
  'com/zeoflow/android/material/drawable',
  'com/zeoflow/android/material/elevation',
  'com/zeoflow/android/material/expandable',
  'com/zeoflow/android/material/floatingactionbutton',
  'com/zeoflow/android/material/imageview',
  'com/zeoflow/android/material/internal',
  'com/zeoflow/android/material/math',
  'com/zeoflow/android/material/menu',
  'com/zeoflow/android/material/navigation',
  'com/zeoflow/android/material/radiobutton',
  'com/zeoflow/android/material/resources',
  'com/zeoflow/android/material/ripple',
  'com/zeoflow/android/material/shape',
  'com/zeoflow/android/material/shadow',
  'com/zeoflow/android/material/slider',
  'com/zeoflow/android/material/snackbar',
  'com/zeoflow/android/material/stateful',
  'com/zeoflow/android/material/switchmaterial',
  'com/zeoflow/android/material/tabs',
  'com/zeoflow/android/material/textfield',
  'com/zeoflow/android/material/textview',
  'com/zeoflow/android/material/theme',
  'com/zeoflow/android/material/theme/overlay',
  'com/zeoflow/android/material/tooltip',
  'com/zeoflow/android/material/transition',
  'com/zeoflow/android/material/transformation',
  'com/zeoflow/android/material/typography',
]

android {
  sourceSets {
    main.manifest.srcFile 'java/com/zeoflow/android/material/AndroidManifest.xml'
    main.java.srcDir 'java'
    main.java.includes = srcDirs.collect { it + '/**/*.java' }
    main.java.excludes = [
      '**/build/**',
    ]
    srcDirs.forEach {
      main.res.srcDirs += 'java/' + it + '/res'
      main.res.srcDirs += 'java/' + it + '/res-public'
    }
  }

  testOptions.unitTests.includeAndroidResources = true

  buildTypes.all {
    consumerProguardFiles 'proguard-behaviors.pro', 'proguard-inflater.pro'
  }

  compileSdkVersion rootProject.ext.compileSdkVersion
  defaultConfig {
    vectorDrawables.useSupportLibrary = true
    minSdkVersion rootProject.ext.minSdkVersion
    targetSdkVersion 28
  }

  testOptions {
    unitTests.returnDefaultValues = true
  }

  aaptOptions {
    additionalParameters "--no-version-vectors"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  dexOptions{
    preDexLibraries = rootProject.ext.preDexLibs
  }
}
